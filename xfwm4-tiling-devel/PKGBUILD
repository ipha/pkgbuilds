# Maintainer: Mitch Bigelow <ipha00@gmail.com>

pkgname=xfwm4-tiling-devel
_realname=xfwm4
_patchver=v3
pkgver=4.11.1
pkgrel=1
pkgdesc="Xfce window manager with corner tiling patch (dev branch)"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=("libxfce4ui>=4.11.0" "libwnck" "hicolor-icon-theme")
makedepends=('intltool')
provides=("${_realname}=${pkgver}")
conflicts=("${_realname}")
replaces=("${_realname}")
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/xfce/${_realname}/4.11/${_realname}-${pkgver}.tar.bz2
		xfwm4-tiling-${_patchver}.patch)
sha256sums=('1a58e385d4d8dda4d6ac3c7b1e94b3d499bcb78619d91004ffa65a3e6bb58f3e'
			'967cdc3f225f3ad24c53b644cf970dfc4b0731c73138d970ec6a0848202bdcee')

prepare() {
	cd "${srcdir}/${_realname}-${pkgver}"

	# Corner tiling patch
	patch -p1 < ${srcdir}/${pkgname}-${_patchver}.patch
}

build() {
	cd "${srcdir}/${_realname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib \
		--localstatedir=/var \
		--disable-static \
		--enable-startup-notification \
		--enable-randr \
		--enable-compositor \
		--enable-xsync \
		--disable-debug

	make
}

package() {
	cd "${srcdir}/${_realname}-${pkgver}"
	make DESTDIR=${pkgdir} install
}
