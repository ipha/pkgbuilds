# Maintainer: Mitch Bigelow <ipha00@gmail.com>

pkgname=wireshark-qt
pkgbase=wireshark
pkgver=1.10.3
pkgrel=1
pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - QT4 frontend'
arch=('i686' 'x86_64')
url='http://www.wireshark.org/'
license=('GPL2')

depends=('krb5' 'libpcap' 'bash' 'gnutls' 'glib2' 'lua' 'portaudio' 'qt4' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('python')

provides=('wireshark-cli')
conflicts=('wireshark-cli' 'wireshark-gtk')
install=wireshark-qt.install
source=(http://www.wireshark.org/download/src/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('c1641b36aeb0ca2c1a95fb01d2c647888b65e00a215c9a6464b5fc61e0fd498c')


prepare() {
	cd ${pkgbase}-${pkgver}

	# Fix rcc path for qt4
	sed -i 's/rcc /rcc-qt4 /' ui/qt/Makefile.am
}

build() {
	cd ${pkgbase}-${pkgver}

	./autogen.sh
	./configure \
		--prefix=/usr \
		--with-qt=yes \
		--with-pcap \
		--with-zlib \
		--with-lua \
		--with-portaudio \
		--with-ssl \
		--with-krb5
	make all
}

package() {
	cd ${pkgbase}-${pkgver}

	make DESTDIR="${pkgdir}" install

	#wireshark uid group is 150
	chgrp 150 "${pkgdir}/usr/bin/dumpcap"
	chmod 754 "${pkgdir}/usr/bin/dumpcap"

	# Headers
	install -dm755 "${pkgdir}"/usr/include/${pkgbase}/{epan/{crypt,dfilter,dissectors,ftypes},wiretap,wsutil}

	install -m644 color.h config.h register.h ws_symbol_export.h "${pkgdir}/usr/include/${pkgbase}"
	for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes wiretap wsutil; do
		install -m644 ${d}/*.h "${pkgdir}"/usr/include/${pkgbase}/${d}
	done

	# Icons
	for d in 16 32 48; do
		install -Dm644 image/hi${d}-app-wireshark.png \
			"${pkgdir}/usr/share/icons/hicolor/${d}x${d}/apps/wireshark.png"
	done

	for d in 16 24 32 48 64 128 256 ; do
		install -Dm644 image/WiresharkDoc-${d}.png \
			"${pkgdir}/usr/share/icons/hicolor/${d}x${d}/mimetypes/application-vnd.tcpdump.pcap.png"
	done

	install -Dm644 wireshark.desktop "${pkgdir}/usr/share/applications/wireshark.desktop"
}
