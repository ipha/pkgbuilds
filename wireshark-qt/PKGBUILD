# Maintainer: Mitch Bigelow <ipha00@gmail.com>

pkgname=wireshark-qt
_pkgbase=wireshark
pkgver=1.10.8
pkgrel=1
pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - QT4'
arch=('i686' 'x86_64')
url='http://www.wireshark.org/'
license=('GPL2')

depends=('krb5' 'libpcap' 'bash' 'gnutls' 'glib2' 'lua' 'portaudio' 'qt4' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('python')

provides=('wireshark-cli')
conflicts=('wireshark-cli' 'wireshark-gtk')
install=wireshark-qt.install
source=(http://www.wireshark.org/download/src/${_pkgbase}-${pkgver}.tar.bz2)
sha256sums=('27d9cf4f0ca1ac436104b7f27caf8ad65f73db09edc8df0742ce9688c7d38763')


prepare() {
	cd ${_pkgbase}-${pkgver}

	# Fix rcc name for qt4
	sed -i 's/rcc /rcc-qt4 /' ui/qt/Makefile.am
	# Fix moc name for qt4
	sed -i 's/AC_PATH_PROG(MOC, moc)/AC_PATH_PROG(MOC, moc-qt4)/' configure.ac
	# Fix uic name for qt4
	sed -i 's/AC_PATH_PROG(UIC, uic)/AC_PATH_PROG(UIC, uic-qt4)/' configure.ac
}

build() {
	cd ${_pkgbase}-${pkgver}

	./autogen.sh
	./configure \
		--prefix=/usr \
		--with-qt=yes \
		--with-pcap \
		--with-zlib \
		--with-lua \
		--with-portaudio \
		--with-ssl \
		--with-krb5
	make all
}

package() {
	cd ${_pkgbase}-${pkgver}

	make DESTDIR="${pkgdir}" install

	#wireshark uid group is 150
	chgrp 150 "${pkgdir}/usr/bin/dumpcap"
	chmod 754 "${pkgdir}/usr/bin/dumpcap"

	# Headers
	install -dm755 "${pkgdir}"/usr/include/${_pkgbase}/{epan/{crypt,dfilter,dissectors,ftypes},wiretap,wsutil}

	install -m644 color.h config.h register.h ws_symbol_export.h "${pkgdir}/usr/include/${_pkgbase}"
	for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes wiretap wsutil; do
		install -m644 ${d}/*.h "${pkgdir}"/usr/include/${_pkgbase}/${d}
	done

	# Icons
	for d in 16 32 48; do
		install -Dm644 image/hi${d}-app-wireshark.png \
			"${pkgdir}/usr/share/icons/hicolor/${d}x${d}/apps/wireshark.png"
	done

	for d in 16 24 32 48 64 128 256 ; do
		install -Dm644 image/WiresharkDoc-${d}.png \
			"${pkgdir}/usr/share/icons/hicolor/${d}x${d}/mimetypes/application-vnd.tcpdump.pcap.png"
	done

	install -Dm644 wireshark.desktop "${pkgdir}/usr/share/applications/wireshark.desktop"
}
