# Maintainer: Mitch Bigelow <ipha00@gmail.com>

pkgname=wireshark-gtk2
pkgbase=wireshark
pkgver=1.10.2
pkgrel=2
pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - GTK2 frontend'
arch=('i686' 'x86_64')
url='http://www.wireshark.org/'
license=('GPL2')

depends=('krb5' 'libpcap' 'bash' 'gnutls' 'glib2' 'lua' 'gtk2' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('python')

provides=('wireshark-cli' 'wireshark-gtk')
conflicts=('wireshark-cli' 'wireshark-gtk')
options=(!libtool)
install=wireshark-gtk2.install
source=(http://www.wireshark.org/download/src/${pkgbase}-${pkgver}.tar.bz2
		52144a.patch
		52144b.patch)
sha256sums=('b7a83314424e13639c591de924aa240bc5f3d9cae01dde69bf858fa04b22a33d'
			'8f8f8cd3ee64510be9a0558d685fbb64eec5bf85ccb0b77d203129b999140fc5'
			'bc15c746043a5a6d4618b961dcc7589a07fc65a6b599b461f8f6870741d8f8b6')


prepare() {
	cd ${pkgbase}-${pkgver}

	# Fix build
	patch -p1 < ${srcdir}/52144a.patch
	patch -p1 < ${srcdir}/52144b.patch
}

build() {
	cd ${pkgbase}-${pkgver}

	./autogen.sh
	./configure \
		--prefix=/usr \
		--with-ssl \
		--with-zlib=yes \
		--with-lua \
		--with-gtk3=no
	make all
}

package() {
	cd ${pkgbase}-${pkgver}

	make DESTDIR="${pkgdir}" install

	#wireshark uid group is 150
	chgrp 150 "${pkgdir}/usr/bin/dumpcap"
	chmod 754 "${pkgdir}/usr/bin/dumpcap"

	# Headers
	install -dm755 "${pkgdir}"/usr/include/${pkgbase}/{epan/{crypt,dfilter,dissectors,ftypes},wiretap,wsutil}

	install -m644 color.h config.h register.h "${pkgdir}/usr/include/${pkgbase}"
	for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes wiretap wsutil; do
		install -m644 ${d}/*.h "${pkgdir}"/usr/include/${pkgbase}/${d}
	done

	# Icons
	for d in 16 32 48; do
		install -Dm644 image/hi${d}-app-wireshark.png "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/apps/wireshark.png"
	done

	for d in 16 24 32 48 64 128 256 ; do
		install -Dm644 image/WiresharkDoc-${d}.png "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/mimetypes/application-vnd.tcpdump.pcap.png"
	done

	install -Dm644 wireshark.desktop "${pkgdir}/usr/share/applications/wireshark.desktop"
}
